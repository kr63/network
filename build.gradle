buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

plugins {
    id 'java'
    id 'com.gradle.build-scan' version '2.2.1'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id "org.flywaydb.flyway" version "5.2.4"
    id 'net.ltgt.apt' version '0.21'
    id 'net.ltgt.apt-idea' version '0.21'
    id 'jacoco'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    mapstructVersion = "1.3.0.Final"
    lombokVersion = "1.18.6"
    junitVersion = "5.4.1"
    jacocoVersion = "0.8.3"
    springSecurityVersion = "2.0.5.RELEASE"
}

group = 'com.example.network'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

bootJar {
    baseName = 'network'
    version = '0.0.1'
}

flyway {
    user = 'dbuser'
    password = 'dbpass'
    url = 'jdbc:postgresql://localhost:5432/dbname'
    schemas = ['dbschema']
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-devtools"

    // security
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.security.oauth.boot:" +
            "spring-security-oauth2-autoconfigure:${springSecurityVersion}"

    // templates
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    
    // spring session
    implementation "org.springframework.session:spring-session-jdbc"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "org.flywaydb:flyway-core"
    implementation "org.postgresql:postgresql"

    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.mapstruct:mapstruct:${mapstructVersion}"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit' //exclude junit4 by both name and group
    }

    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
}

compileJava.dependsOn(processResources)

jacoco {
    toolVersion = jacocoVersion
}
test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
